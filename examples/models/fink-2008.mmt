[[model]]
name: fink-2008
display_name: Fink et al., 2006
version: 20210816
mmt_authors: Michael Clerx, Chon Lok Lei
desc: """
    Fink 2008 model of the human ventricle, based on TTP-2006.

    Adapted from CellML. The original meta data follows below.

    ---------------------------------------------------------------------------

    Contributions of HERG potassium+ current to repolarization of the human
    ventricular action potential

    Catherine Lloyd
    Bioengineering Institute, University of Auckland

    This model is known to run in OpenCell and COR to reproduce the published
    results. Units have been checked. COR reports they are balanced and
    consistent while OpenCell is reporting some inconsistencies. OpenCell is
    currently having unit "issues" and we believe the units are correct in this
    CellML model.

    ABSTRACT:
    Action potential repolarization in the mammalian heart is governed by
    interactions of a number of time- and voltage-dependent channel-mediated
    currents, as well as contributions from the sodium+/Ca2+ exchanger and the
    sodium+/potassium+ pump. Recent work has shown that one of the potassium+
    currents (HERG) which contributes to repolarization in mammalian ventricle
    is a locus at which a number of point mutations can have significant
    functional consequences. In addition, the remarkable sensitivity of this
    potassium+ channel isoform to inhibition by a variety of pharmacological
    agents and clinical drugs has resulted in HERG being a major focus for
    Safety Pharmacology requirements. For these reasons we and others have
    attempted to define the functional role for HERG-mediated potassium+
    currents in repolarization of the action potential in the human ventricle.
    Here, we describe and evaluate changes in the formulations for two
    potassium+ currents, ik1 and HERG (or IK,r), within the framework of ten
    Tusscher model of the human ventricular action potential. In this
    computational study, new mathematical formulations for the two nonlinear
    potassium+ conductances, ik1 and HERG, have been developed based upon
    experimental data obtained from electrophysiological studies of excised
    human ventricular tissue and/or myocytes. The resulting mathematical model
    provides much improved simulations of the relative sizes and time courses
    of the potassium+ currents which modulate repolarization. Our new
    formulation represents an important first step in defining the mechanism(s)
    of repolarization of the membrane action potential in the human ventricle.
    Our overall goal is to understand the genesis of the T-wave of the human
    electrocardiogram.

    The complete original paper reference is cited below:

    Contributions of HERG potassium+ current to repolarization of the human
    ventricular action potential
    Martin Fink, Denis Noble, Laszlo Virag, Andras Varro, and Wayne R. Giles
    2008 Progress in Biophysics and Molecular Biology 96, (1-3), 357-376
    """
# Initial values
membrane.V    = -86.45
ikr.Cr1       = 0.9786
ikr.Cr2       = 0.0031
ikr.Cr3       = 0.0029
ikr.Or4       = 0.014
ikr.Ir5       = 0.0014
ikr.BCr1      = 0
ikr.BCr2      = 0
ikr.BCr3      = 0
ikr.BOr4      = 0
ikr.BIr5      = 0
iks.Xs        = 0.00303
ito.s         = 1
ito.r         = 2.11e-08
ina.m         = 0.00132
ina.h         = 0.7768
ina.j         = 0.7766
ical.d        = 5.06e-06
ical.f        = 0.9999
ical.f2       = 0.9995
ical.fCass    = 1
calcium.Ca_i  = 0.0001092
calcium.Ca_SR = 2.7656
calcium.Ca_ss = 0.0001893
irel.R_prime  = 0.9864
sodium.Na_i   = 7.940167
potassium.K_i = 141.0167

[engine]
time = 0 [ms]
    in [ms]
    bind time
pace = 0
    bind pace

#
# Membrane potential
#
[membrane]
dot(V) = -(i_ion + stimulus.i_stim)
    in [mV]
    label membrane_potential
i_ion = ik1.i_K1 + ito.i_to + ikr.i_Kr + iks.i_Ks + ical.i_CaL + inak.i_NaK + ina.i_Na + inab.i_b_Na + inaca.i_NaCa + icab.i_b_Ca + ipk.i_p_K + ipca.i_p_Ca
    in [A/F]
    label cellular_current

#
# Stimulus current
#
[stimulus]
i_stim = engine.pace * stim_amplitude
    in [A/F]
stim_amplitude = -12 [A/F]
    in [A/F]

#
# Cell properties
#
[cell]
Cm = 0.115 [nF]
    in [nF]
Vol_c = 0.016404 [nL]
    in [nL]

#
# Calcium concentrations
#
[calcium]
dot(Ca_SR) = buffer.Ca_sr_bufsr * (ileak.i_up - (irel.i_rel + ileak.i_leak))
    in [mM]
dot(Ca_i) = buffer.Ca_i_bufc * ((ileak.i_leak - ileak.i_up) * V_sr / cell.Vol_c + ileak.i_xfer - (icab.i_b_Ca + ipca.i_p_Ca - 2 * inaca.i_NaCa) * cell.Cm / (2 * cell.Vol_c * phys.F))
    in [mM]
dot(Ca_ss) = buffer.Ca_ss_bufss * (-ical.i_CaL * cell.Cm / (2 * V_ss * phys.F) + irel.i_rel * V_sr / V_ss - ileak.i_xfer * cell.Vol_c / V_ss)
    in [mM]
V_sr = 0.001094 [nL]
    in [nL]
V_ss = 5.468e-05 [nL]
    in [nL]

#
# Calcium buffering
#
[buffer]
Buf_c = 0.2 [mM]
    in [mM]
Buf_sr = 10 [mM]
    in [mM]
Buf_ss = 0.4 [mM]
    in [mM]
Ca_i_bufc = 1 / (1 + Buf_c * K_buf_c / (calcium.Ca_i + K_buf_c) ^ 2)
Ca_sr_bufsr = 1 / (1 + Buf_sr * K_buf_sr / (calcium.Ca_SR + K_buf_sr) ^ 2)
Ca_ss_bufss = 1 / (1 + Buf_ss * K_buf_ss / (calcium.Ca_ss + K_buf_ss) ^ 2)
K_buf_c = 0.001 [mM]
    in [mM]
K_buf_sr = 0.3 [mM]
    in [mM]
K_buf_ss = 0.00025 [mM]
    in [mM]

#
# External ion concentrations
#
[ion]
Ca_o = 2 [mM]
    in [mM]
K_o = 5.4 [mM]
    in [mM]
Na_o = 140 [mM]
    in [mM]

#
# Physical constants
#
[phys]
F =  9.64853415e4 [C/mol]
    in [C/mol]
R = 8314.472 [mJ/mol/K]
    in [mJ/mol/K]
T = 310 [K]
    in [K]

#
# L-type calcium current
#
[ical]
use membrane.V
i_CaL = g_CaL * d * f * f2 * fCass * 4 * (V - 15 [mV]) * phys.F ^ 2 / (phys.R * phys.T) * (0.25 * calcium.Ca_ss * exp(2 * (V - 15 [mV]) * phys.F / (phys.R * phys.T)) - ion.Ca_o) / (exp(2 * (V - 15 [mV]) * phys.F / (phys.R * phys.T)) - 1)
    in [A/F]
g_CaL = 2e-5 [L/F/ms]
    in [L/F/ms]
dot(d) = (d_inf - d) / tau_d
    d_inf = 1 / (1 + exp((d_inf_shift - V) / 7.5 [mV]))
    d_inf_shift = 5 [mV]
        in [mV]
    alpha_d = 1.4 / (1 + exp((-35 [mV] - V) / 13 [mV])) + 0.25
    beta_d = 1.4 / (1 + exp((V + 5 [mV]) / 5 [mV]))
    gamma_d = 1 / (1 + exp((50 [mV] - V) / 20 [mV]))
    tau_d = 1 [ms] * (alpha_d * beta_d + gamma_d)
        in [ms]
dot(f2) = (f2_inf - f2) / tau_f2
    f2_inf = 0.75 / (1 + exp((V + 35 [mV]) / 7 [mV])) + 0.25
    tau_f2 = (562 [ms] * exp(-(V + 27 [mV]) ^ 2 / 240 [mV^2]) + 31 [ms] / (1 + exp((25 [mV] - V) / 10 [mV])) + 80 [ms] / (1 + exp((V + 30 [mV]) / 10 [mV]))) / 2
        in [ms]
dot(fCass) = (fCass_inf - fCass) / tau_fCass
    fCass_inf = 0.4 / (1 + (calcium.Ca_ss / 0.05 [mM]) ^ 2) + 0.6
    tau_fCass = 80 [ms] / (1 + (calcium.Ca_ss / 0.05 [mM]) ^ 2) + 2 [ms]
        in [ms]
dot(f) = (f_inf - f) / tau_f
    f_inf = 1 / (1 + exp((V + 20 [mV]) / 7 [mV]))
    tau_f = (1102.5 [ms] * exp(-(V + 27 [mV]) ^ 2 / 225 [mV^2]) + 200 [ms] / (1 + exp((13 [mV] - V) / 10 [mV])) + 180 [ms] / (1 + exp((V + 30 [mV]) / 10 [mV])) + 20 [ms]) / 4
        in [ms]

#
# Background calcium current
#
[icab]
use membrane.V
g_bca = 0.0004736 [mS/uF]
    in [mS/uF]
i_b_Ca = g_bca * (V - rev.E_Ca)
    in [A/F]

#
# Time independent potassium current
#
[ik1]
use membrane.V
i_K1 = g_K1 * sqrt(ion.K_o / 5.4 [mM]) * xK1_inf * (V - rev.E_K)
    in [A/F]
g_K1 = g_K1_0 * (phys.T / 35 [K] - 55 / 7)
    in [mS/uF]
g_K1_0 = 0.6821 [mS/uF]
    in [mS/uF]
xK1_inf = phi * rec1 + (1 - phi) * rec2
    KbMg = 0.45 [mM] * exp(-(V - fac * rev.E_K) / 20 [mV])
        in [mM]
    Kd1SPM = 0.0007 [mM] * exp(-(V - fac * rev.E_K + 8 [V/M] * Mg_Buf) / 4.8 [mV])
        in [mM]
    Kd2SPM = 0.04 [mM] * exp(-(V - fac * rev.E_K) / 9.1 [mV])
        in [mM]
    KiMg = 2.8 [mM] * exp(-(V - fac * rev.E_K) / 180 [mV])
        in [mM]
    Mg_Buf = 0.0356 [mM]
        in [mM]
    SPM = 0.0014613 [mM]
        in [mM]
    fac = 1.0648
    phi = 0.8838
    rec1 = temp^2 / (SPM / Kd1SPM + Mg_Buf / KiMg + temp^3)
    rec2 = 1 / (1 + SPM / Kd2SPM)
    temp = 1 + Mg_Buf / KbMg


#
# Rapidly activating potassium current
#
[ikr]
use membrane.V
g_Kr_0 = 0.024 [mS/uF]
    in [mS/uF]
g_Kr = g_Kr_0 * (phys.T / 35 [K] - 55 / 7)
    in [mS/uF]
i_Kr = g_Kr * sqrt(ion.K_o / 5.4 [mM]) * Or4 * (V - rev.E_K)
    in [A/F]
# Markov model
T_Base = 310 [K]
    in [K]
alpha_xr1 = 1 [1/ms] * phys.T / T_Base * exp(24.335 + T_Base / phys.T * (0.0112 [1/mV] * V - 25.914))
    in [1/ms]
alpha_xr2 = 1 [1/ms] * phys.T / T_Base * exp(22.746 + T_Base / phys.T * (0 [1/mV] * V - 25.914))
    in [1/ms]
alpha_xr3 = 1 [1/ms] * phys.T / T_Base * exp(22.098 + T_Base / phys.T * (0.0365 [1/mV] * V - 25.914))
    in [1/ms]
alpha_xr4 = 1 [1/ms] * phys.T / T_Base * exp(30.016 + T_Base / phys.T * (0.0223 [1/mV] * V - 30.888)) * (5.4 [mM] / ion.K_o) ^ 0.4
    in [1/ms]
beta_xr1 = 1 [1/ms] * phys.T / T_Base * exp(13.688 + T_Base / phys.T * (-0.0603 [1/mV] * V - 15.707))
    in [1/ms]
beta_xr2 = 1 [1/ms] * phys.T / T_Base * exp(13.193 + T_Base / phys.T * (0 [1/mV] * V - 15.707))
    in [1/ms]
beta_xr3 = 1 [1/ms] * phys.T / T_Base * exp(7.313 + T_Base / phys.T * (-0.0399 [1/mV] * V - 15.707))
    in [1/ms]
beta_xr4 = 1 [1/ms] * phys.T / T_Base * exp(30.061 + T_Base / phys.T * (-0.0312 [1/mV] * V - 33.243))
    in [1/ms]
dot(Cr1) = beta_xr1 * Cr2 - alpha_xr1 * Cr1
dot(Cr2) = alpha_xr1 * Cr1 + beta_xr2 * Cr3 - (alpha_xr2 + beta_xr1) * Cr2
dot(Cr3) = alpha_xr2 * Cr2 + beta_xr3 * Or4 - (alpha_xr3 + beta_xr2) * Cr3
dot(Ir5) = alpha_xr4 * Or4 - beta_xr4 * Ir5
dot(Or4) = alpha_xr3 * Cr3 + beta_xr4 * Ir5 - (alpha_xr4 + beta_xr3) * Or4 - OtoB + BtoO
# Sotalol block
BtoO = BOr4 * kDiss
    in [1/ms]
OtoB = Or4 * Sotalol_mM * kBinding
    in [1/ms]
Sotalol_mM = 0 [mM]
    in [mM]
kBinding = 0.005 [1/mM/ms]
    in [1/mM/ms]
kDiss = 0.00125 [1/ms]
    in [1/ms]
dot(BCr1) = beta_xr1 * BCr2 - alpha_xr1 * BCr1
dot(BCr2) = alpha_xr1 * BCr1 + beta_xr2 * BCr3 - (alpha_xr2 + beta_xr1) * BCr2
dot(BCr3) = alpha_xr2 * BCr2 + beta_xr3 * BOr4 - (alpha_xr3 + beta_xr2) * BCr3
dot(BIr5) = alpha_xr4 * BOr4 - beta_xr4 * BIr5
dot(BOr4) = alpha_xr3 * BCr3 + beta_xr4 * BIr5 - (alpha_xr4 + beta_xr3) * BOr4 + OtoB - BtoO

#
# Slowly activating potassium current
#
[iks]
use membrane.V
g_Ks = 0.0392 [mS/uF]
    in [mS/uF]
i_Ks = g_Ks * Xs ^ 2 * (V - rev.E_Ks)
    in [A/F]
dot(Xs) = (xs_inf - Xs) / tau_xs
    alpha_xs = 1400 / sqrt(1 + exp((5 [mV] - V) / 6 [mV]))
    beta_xs = 1 / (1 + exp((V - 35 [mV]) / 15 [mV]))
    xs_inf = 1 / (1 + exp((-5 [mV] - V) / 14 [mV]))
    tau_xs = 1 [ms] * alpha_xs * beta_xs + 80 [ms]
        in [ms]

#
# Fast sodium current
#
[ina]
use membrane.V
g_Na = 11 [mS/uF]
    in [mS/uF]
i_Na = g_Na * m ^ 3 * h * j * (V - rev.E_Na)
    in [A/F]
shift = 0 [mV]
    in [mV]
dot(m) = (inf - m) / tau
    inf = 1 / (1 + exp((-56.86 [mV] - V) / 9.03 [mV])) ^ 2
    a = 1 / (1 + exp((-60 [mV] - V) / 5 [mV]))
    b = 0.1 / (1 + exp((V + 35 [mV]) / 5 [mV])) + 0.1 / (1 + exp((V - 50 [mV]) / 200 [mV]))
    tau = 1 [ms] * a * b
        in [ms]
dot(h) = (inf - h) / tau
    inf = 1 / (1 + exp((V + 71.55 [mV] - shift) / 7.43 [mV])) ^ 2
    a = if(V < -40 [mV] + shift, 0.057 * exp(-(V + 80 [mV] - shift) / 6.8 [mV]), 0)
    b = if(V < -40 [mV] + shift, 2.7 * exp(0.079 [1/mV] * (V - shift)) + 310000 * exp(0.3485 [1/mV] * (V - shift)), 0.77 / (0.13 * (1 + exp((V + 10.66 [mV] - shift) / -11.1 [mV]))))
    tau = 1 [ms] / (a + b)
        in [ms]
dot(j) = (inf - j) / tau
    inf = 1 / (1 + exp((V + 71.55 [mV] - shift) / 7.43 [mV])) ^ 2
    a = if(V < -40 [mV] + shift, (-25428 * exp(0.2444 [1/mV] * (V - shift)) - 6.948e-06 * exp(-0.04391 [1/mV] * (V - shift))) * (V + 37.78 [mV]) / 1 [mV] / (1 + exp(0.311 [1/mV] * (V + 79.23 [mV] - shift))), 0)
    b = if(V < -40 [mV] + shift, 0.02424 * exp(-0.01052 [1/mV] * (V - shift)) / (1 + exp(-0.1378 [1/mV] * (V + 40.14 [mV] - shift))), 0.6 * exp(0.057 [1/mV] * (V - shift)) / (1 + exp(-0.1 [1/mV] * (V + 32 [mV] - shift))))
    tau = 1 [ms] / (a + b)
        in [ms]

#
# Sodium-Calcium exchanger
#
[inaca]
use membrane.V
i_NaCa = K_NaCa * (exp(gamma * V * phys.F / (phys.R * phys.T)) * sodium.Na_i ^ 3 * ion.Ca_o - exp((gamma - 1) * V * phys.F / (phys.R * phys.T)) * ion.Na_o ^ 3 * calcium.Ca_i * alpha) / ((Km_Nai ^ 3 + ion.Na_o ^ 3) * (Km_Ca + ion.Ca_o) * (1 + K_sat * exp((gamma - 1) * V * phys.F / (phys.R * phys.T))))
    in [A/F]
K_NaCa = 200 [A/F]
    in [A/F]
K_sat = 0.1
Km_Ca = 1.38 [mM]
    in [mM]
Km_Nai = 87.5 [mM]
    in [mM]
alpha = 2.5
gamma = 0.35

#
# Sodium-potassium pump
#
[inak]
use membrane.V
i_NaK = P_NaK * ion.K_o / (ion.K_o + K_mk) * sodium.Na_i / (sodium.Na_i + K_mNa) / (1 + 0.1245 * exp(-0.1 * V * phys.F / (phys.R * phys.T)) + 0.0353 * exp(-V * phys.F / (phys.R * phys.T)))
    in [A/F]
K_mNa = 40 [mM]
    in [mM]
K_mk = 1 [mM]
    in [mM]
P_NaK = 1.297 [A/F]
    in [A/F]

#
# Background sodium current
#
[inab]
use membrane.V
g_bna = 0.00029 [1/ms]
    in [1/ms]
i_b_Na = g_bna * (V - rev.E_Na)
    in [A/F]

#
# Calcium leak, uptake, transfer
#
[ileak]
i_leak = Vol_leak * (calcium.Ca_SR - calcium.Ca_i)
    in [mM/ms]
i_up = Vmax_up / (1 + K_up ^ 2 / calcium.Ca_i ^ 2)
    in [mM/ms]
i_xfer = Vol_xfer * (calcium.Ca_ss - calcium.Ca_i)
    in [mM/ms]
K_up = 0.00025 [mM]
    in [mM]
Vmax_up = 0.006375 [mM/ms]
    in [mM/ms]
Vol_leak = 0.00036 [1/ms]
    in [1/ms]
Vol_xfer = 0.0038 [1/ms]
    in [1/ms]

#
# Sarcolemmal calcium pump
#
[ipca]
i_p_Ca = g_pCa * calcium.Ca_i / (calcium.Ca_i + K_pCa)
    in [A/F]
K_pCa = 0.0005 [mM]
    in [mM]
g_pCa = 0.0619 [A/F]
    in [A/F]

#
# Plateau potassium current
#
[ipk]
use membrane.V
i_p_K = g_pK * (V - rev.E_K) / (1 + exp((25 [mV] - V) / 5.98 [mV]))
    in [A/F]
g_pK = 0.00973 [mS/uF]
    in [mS/uF]

#
# Calcium release current
#
[irel]
i_rel = Vol_rel * O * (calcium.Ca_SR - calcium.Ca_ss)
    in [mM/ms]
dot(R_prime) = -k2 * calcium.Ca_ss * R_prime + k4 * (1 - R_prime)
O = k1 * calcium.Ca_ss ^ 2 * R_prime / (k3 + k1 * calcium.Ca_ss ^ 2)
EC = 1.5 [mM]
    in [mM]
Vol_rel = 0.306 [1/ms]
    in [1/ms]
k1_prime = 0.15 [1/mM^2/ms]
    in [1/mM^2/ms]
k2_prime = 0.045 [1/mM/ms]
    in [1/mM/ms]
k1 = k1_prime / kcasr
    in [1/mM^2/ms]
k2 = k2_prime * kcasr
    in [1/mM/ms]
k3 = 0.06 [1/ms]
    in [1/ms]
k4 = 0.005 [1/ms]
    in [1/ms]
kcasr = max_sr - (max_sr - min_sr) / (1 + (EC / calcium.Ca_SR) ^ 2)
    max_sr = 2.5
    min_sr = 1

#
# Transient potassium current
#
[ito]
use membrane.V
i_to = g_to * r * s * (V - rev.E_K)
    in [A/F]
g_to = 0.2 [mS/uF]
    in [mS/uF]
dot(r) = (inf - r) / tau
    inf = 1 / (1 + exp((20 [mV] - V) / 6 [mV]))
    tau = 9.5 [ms] * exp(-(V + 40 [mV]) ^ 2 / 1800 [mV^2]) + 0.8 [ms]
        in [ms]
dot(s) = (inf - s) / tau
    inf = 1 / (1 + exp((V + 20 [mV]) / 5 [mV]))
    tau = 85 [ms] * exp(-(V + 45 [mV]) ^ 2 / 320 [mV^2]) + 5 [ms] / (1 + exp((V - 20 [mV]) / 5 [mV])) + 3 [ms]
        in [ms]

#
# Potassium concentration
#
[potassium]
dot(K_i) = -(ik1.i_K1 + ito.i_to + ikr.i_Kr + iks.i_Ks + ipk.i_p_K + stimulus.i_stim - 2 * inak.i_NaK) / (cell.Vol_c * phys.F) * cell.Cm
    in [mM]

#
# Sodium concentration
#
[sodium]
dot(Na_i) = -(ina.i_Na + inab.i_b_Na + 3 * inak.i_NaK + 3 * inaca.i_NaCa) * cell.Cm / (cell.Vol_c * phys.F)
    in [mM]

#
# Reversal potentials
#
[rev]
E_Ca = 0.5 * phys.R * phys.T / phys.F * log(ion.Ca_o / calcium.Ca_i)
    in [mV]
E_K = phys.R * phys.T / phys.F * log(ion.K_o / potassium.K_i)
    in [mV]
E_Ks = phys.R * phys.T / phys.F * log((ion.K_o + P_kna * ion.Na_o) / (potassium.K_i + P_kna * sodium.Na_i))
    in [mV]
E_Na = phys.R * phys.T / phys.F * log(ion.Na_o / sodium.Na_i)
    in [mV]
P_kna = 0.03

[[protocol]]
# Level  Start    Length   Period   Multiplier
1.0      100.0    3.0      1000.0   0

[[script]]
import matplotlib.pyplot as plt
import myokit

# Get model and protocol, create simulation
m = get_model()
p = get_protocol()
s = myokit.Simulation(m, p)

# Run simulation
d = s.run(1000)

# Display the results
plt.figure()
plt.xlabel('Time (ms)')
plt.ylabel('Membrane potential (mV)')
plt.plot(d.time(), d['membrane.V'])
plt.show()

